DELIMITERS = "<.>" "<!>" "<?>" "<...>" "<¶>" ;

SOFT-DELIMITERS = "<,>" ;

SUBREADINGS = LTR ;

SETS

LIST BOS = (>>>) ;
LIST EOS = (<<<) ;

LIST A = adj ;
LIST Adv = adv ;
LIST PreAdv = preadv ;
LIST Pron = prn ;
LIST N = n ;
LIST NP = np ;
LIST V = vblex ;
LIST Vall = vblex vbmod vbser vbhaver ;
LIST Det = det ;
LIST Def = def ;
LIST Ind = ind ;
LIST CC = cnjcoo ;
LIST CS = cnjsub ;
LIST CA = cnjadv ;
LIST Interj = ij ;
LIST Num = num ;
LIST Cm = cm ;
LIST Lpar = lpar ;
LIST Pr = pr ; 

LIST Pos = pos ; 

SET Ant = (np ant) ;
SET Cog = (np cog) ;
SET Ant_Cog = Ant OR Cog ;
SET Top = (np top) OR (np loc) ;
SET Al = (al) ;

LIST Sg = sg ; 
LIST Pl = pl ; 
LIST M = m ;
LIST F = f ;

LIST Masc = m mf ;
LIST Fem = f mf ;
#LIST Sg = sg sp ;
#LIST Pl = pl sp ;
SET MascSg = (m sg) OR (m sp) OR (mf sg) OR (mf sp) ;
SET MascPl = (m pl) OR (m sp) OR (mf pl) OR (mf sp) ;
SET FemSg = (f sg) OR (f sp) OR (mf sg) OR (mf sp) ;
SET FemPl = (f pl) OR (f sp) OR (mf pl) OR (mf sp) ;

SET Aord = (adj ord) ;

LIST P1 = (p1) ;
LIST P2 = (p2) ;
LIST P3 = (p3) ;

LIST unknown = ("\\*.*"r) ;
LIST unknownMajIni = ("\\*[A-ZÀÁÈÉíÒÓÚÜÇ].*"r) ;
LIST MajIni = ("<[A-ZÀÁÈÉíÒÓÚÜÇ].*>"r) ;
LIST MinIni = ("<[a-zàáèéìíòóùúçñ].*>"r) ;
LIST MajIni2 = ("<[A-ZÀÁÈÉÍÌÒÓÚÜÇ][a-zàáèéìíòóùúïüçñ·]+.*>"r) ;
LIST segles_num_romans = ("[IVX][IVX]*"r) ;
LIST Upper = "[A-Z][a-z]+"r ;

LIST Avere = vbhaver ;
LIST Ser = vbser ;
LIST Essere = "essere" ;

LIST PrfPrc = pp ;
LIST Ifi = ifi ;

LIST Sotto = "sotto" ;
LIST Io = "io" ;
LIST Stato = "stato" ;
LIST San = "San" ;
LIST Dallo = "dallo" ;
LIST Dai = "dai" ;
LIST Dei = "dei" ;
LIST Puo_essere = "può essere" ;
LIST Da = "da" ;
LIST In = "in" ; 

LIST Interr = itg ;

LIST Imper = imp ;

LIST Excl = "!" ;

LIST Nsg = (n sg) ;
LIST Npl = (n pl) ;

LIST Sg1 = (p1 sg) ;
LIST Sg2 = (p2 sg) ;
LIST Pl3 = (p3 pl) ;

LIST Vconj = (vblex p1) (vblex p2) (vblex p3) ;	# verb conjugat
LIST Vinterr = ("domandare") ("chiedere") ;

# coincidències nom propi/comú
SET NomPropiComu = ("Sala") OR ("Guerra") OR ("Guardia");

SET Dignità = ("presidente") OR ("governatore") OR ("imperatore") OR ("re") OR ("principe") OR ("barone") OR ("conte") OR ("visconte") OR ("marquese") OR ("duca") OR ("arciduca") OR ("vescovo") OR ("arcivescovo");

# All possible word categories

SET WORD = N | V | A | Pron | Det | Adv | CC | CS | Interj | Num | ("\?") | ("!") ;

SET PRE-N =  A | Det | Num | CC ;

SET MARK = Cm | ("\\") | ("\;") ;

SET WORDMARK = WORD | MARK ;

SET N-MOD = A | Det | Num ;

# Categories which cannot be part of a noun phrase

SET NPNH = WORDMARK - PRE-N ;
SET NPNHA = WORDMARK - PRE-N - Adv ;
SET NOT-ADV = WORDMARK - Adv ;


# Barriers

SET S-BOUNDARY = CS | Interr | EOS;

SECTION

SELECT Pron IF (0 Io) ;

SELECT Pr IF (0 Sotto) (1 Det) ;

SELECT Avere IF (1 PrfPrc) ;
SELECT Avere IF (1 Adv) (2 PrfPrc) ;

SELECT Ser IF (0 Puo_essere) (1 PrfPrc) ;
SELECT Ser IF (0 Puo_essere) (1 A) ;

SELECT PrfPrc IF (-1 Essere) (0 Stato) ;

SELECT PrfPrc IF (-1 Stato LINK -1 Essere) ;

#SELECT Pron IF (0C Pron OR Det) (NOT 1* N BARRIER NPNHA) ; -- aquesta regla genera infinitat d'errors

SECTION

REMOVE Imper IF (NOT 1* Excl) ;

SELECT Essere IF (0 Sg1) ;

#REMOVE V IF (0 ("dai")) ;	# dai: distanza dai subordinati. WARNING: No funciona no sé perquè
#REMOVE V IF (0 Dai) ;	# dai: distanza dai subordinati. WARNING: No funciona no sé perquè
#SELECT Pr IF (0 Dai) ;	# dai: distanza dai subordinati. WARNING: No funciona no sé perquè
REMOVE V IF (0 Sg2) (0/* Pr) ;	# dai (alternativa bèstia però funciona)

REMOVE N IF (0 ("dei")) ;	# dei: la rivolta dei Fasci siciliani. WARNING: No funciona no sé perquè
REMOVE N IF (0 N) (0/* Pr) ;	# dei (alternativa bèstia però funciona)

SELECT NP IF (0 San) (1 Upper) ;
REMOVE NP IF (-1 BOS) (0 ("<Primo>")) (1 MinIni) ;

SELECT Det IF (-1 Pr) (0 A OR Det) (NOT 0 ("I")) (1 N) ;

REMOVE A IF (NOT -1 Det + Ind) (0 Det) (0 A) (1C N) ;
  # mia mamma
  # un suo libro

SELECT Det IF (0 Det) (0 Pron) (1 N) (NOT 0 ("un altro")) (NOT 0 ("loro")) (NOT 0 ("molto")) (NOT 1 ("un altro")) ;	# problemes: per saldare alcuni debiti bellici *lo* vendette a re Ladislao; conferendo loro valori nuovi
SELECT Det IF (0 Det) (0 Pron) (1 N) (2 A) (NOT 0 ("un altro")) (NOT 0 ("loro")) (NOT 0 ("molto")) (NOT 1 ("un altro")) ;	# problema: le altre pinne grigie
SELECT Det IF (0 Det) (0 Pron) (1 A) (2 N) (NOT 0 ("un altro")) (NOT 0 ("loro")) (NOT 0 ("molto")) (NOT 1 ("un altro")) ;	# problema: le altre pinne grigie

SELECT PreAdv IF (-1 N + $$MascSg) (0 PreAdv) (1 A + $$MascSg) ;
SELECT PreAdv IF (-1 N + $$MascPl) (0 PreAdv) (1 A + $$MascPl) ;
SELECT PreAdv IF (-1 N + $$FemSg) (0 PreAdv) (1 A + $$FemSg) ;
SELECT PreAdv IF (-1 N + $$FemPl) (0 PreAdv) (1 A + $$FemPl) ;

SELECT Det IF (0 Det) (0 Pron) (1 unknown) ;
SELECT Det (-1 BOS) (0 ("I")) (NOT 1 EOS) ;	# .I movimentos
SELECT Num (NOT -1 BOS) (0 ("I")) (1C N + Sg OR A + Sg) ;	# del secolo I a.C.


SELECT N IF (0 N) (0 V) (-1 A) (1 Pr) ;		# le più grandi collezioni di pesci
SELECT N IF (0 N) (0 A) (-1 Def) (1 ("più")) ;	# il comune più estesso

REMOVE Pr IF (0 Adv) (0 Pr) (-1 V) (1 CC) ;	# andavano su e giù

REMOVE A IF (0 A) (0 N) (-1 Det) (NOT -1 ("molto")) (1 EOS) ; 	# una forza valida nel mondo. WARNING: No funciona per a "nel", però sí per a "el" (problema CG)
REMOVE A IF (0 A) (0 N) (-1/* Pr) (1 EOS) ; 	# solució problema "nel" (a més tracta el cas: Pr N/A.
REMOVE PrfPrc IF (0 PrfPrc) (0 N) (NOT -1 ("molto")) (-1 Det) (1 EOS) ;
REMOVE PrfPrc IF (0 PrfPrc) (0 N) (-1/* Pr) (1 EOS) ;

SELECT N IF (0 A) (NOT 0 Aord) (0 N) (-1 Det) (1 A) (1 N) (2 EOS) ; 		# dello Stato spagnolo. / il primo amico
SELECT N IF (0 A) (NOT 0 Aord) (0 N) (-1/* Pr) (1 A) (1 N) (2 EOS) ; 		# dello Stato spagnolo. / il primo amico
SELECT A IF (0 A) (NOT -1 Aord) (0 N) (-2 Det) (-1 N) (1 EOS) ; 		# dello Stato spagnolo. / il primo amico
SELECT A IF (0 A) (NOT -1 Aord) (0 N) (-2/* Pr) (-1 N) (1 EOS) ; 		# dello Stato spagnolo. / il primo amico

SELECT N IF (0 A) (0 N) (-1 Det) (1 A) (1 N) (2 Cm) ; 		# allo stato fossile,
SELECT N IF (0 A) (0 N) (-1/* Pr) (1 A) (1 N) (2 Cm) ; 		# allo stato fossile,
SELECT A IF (0 A) (0 N) (-2 Det) (-1 N) (1 Cm) ; 		# allo stato fossile,
SELECT A IF (0 A) (0 N) (-2/* Pr) (-1 N) (1 Cm) ; 		# allo stato fossile,

SELECT N IF (0 A) (0 N) (-1 Det) (0 A) (0 N) (1 Vall) ; 	# il comune nacque
SELECT N IF (0 A) (0 N) (-1/* Pr) (0 A) (0 N) (1 Vall) ; 	# il comune nacque
SELECT N IF (0 A) (0 N) (-1 Det) (0 A) (0 N) (1 Adv) (2 Vall) ; 	# il comune non nacque
SELECT N IF (0 A) (0 N) (-1/* Pr) (0 A) (0 N) (1 Adv) (2 Vall) ; 	# il comune non nacque

SELECT N IF (-1 Det + $$MascSg) (0 A + $$MascSg) (0 N + $$MascSg) (NOT 1 N + $$MascSg) ;
SELECT N IF (-1 Det + $$FemSg) (0 A + $$FemSg) (0 N + $$FemSg) (NOT 1 N + $$FemSg) ;
SELECT N IF (-1 Det + $$MascPl) (0 A + $$MascPl) (0 N + $$MascPl) (NOT 1 N + $$MascPl) ;	# distinguere gli ambiti e usi
SELECT N IF (-1 Det + $$FemPl) (0 A + $$FemPl) (0 N + $$FemPl) (NOT 1 N + $$FemPl) ;

SELECT N IF (0 A) (0 N) (-1 Det) (1 A) (1 N) (2 Lpar) ; 	#
SELECT N IF (0 A) (0 N) (-1/* Pr) (1 A) (1 N) (2 Lpar) ; 	#
SELECT A IF (0 A) (0 N) (-2 Det) (-1 N) (1 Lpar) ; 		# 
SELECT A IF (0 A) (0 N) (-2/* Pr) (-1 N) (1 Lpar) ; 		# per miglio quadrato (228,5/km²)

SELECT N IF (0 A) (0 N) (-1 Det) (1C A) ; 			# dello Stato Pontificio
SELECT N IF (0 A) (0 N) (-1/* Pr) (1C A) ; 			# dello Stato Pontificio

SELECT N IF (0 A) (NOT 0 Aord) (0 N) (-1 Det) (1 A) (1 N) (2C Pr) ; 	# dello stato messicano di Coahuila / nei primi decenni
SELECT N IF (0 A) (NOT 0 Aord) (0 N) (-1/* Pr) (1 A) (1 N) (2C Pr) ; 	# dello stato messicano di Coahuila / nei primi decenni
SELECT A IF (0 Aord) (0 N) (-1 Det) (1 A) (1 N) (2C Pr) ; 		# dello stato messicano di Coahuila / nei primi decenni
SELECT A IF (0 Aord) (0 N) (-1/* Pr) (1 A) (1 N) (2C Pr) ; 		# dello stato messicano di Coahuila / nei primi decenni
SELECT A IF (0 A) (0 N) (-2 Det) (-1 N) (1 Pr) (NOT 1 ("da")) ;		# dello stato messicano di Coahuila
SELECT A IF (0 A) (0 N) (-2/* Pr) (-1 N) (1 Pr) (NOT 1 ("da")) ;	# dello stato messicano di Coahuila
REMOVE N IF (0 A) (0 N) (-2 Det) (-1 N) (1 Pr) (1 ("da")); 		# il titolo assunto da Filippo
REMOVE A IF (0 A) (0 PrfPrc) (-2 Det) (-1 N) (1 Pr) (1 ("da")) ;	# il titolo assunto da Filippo

SELECT N IF (0 N OR A) (-1C Aord OR Det) (1 EOS OR Cm OR Pr OR CS OR CA) ;	# nei primi decenni del Novecento

SELECT N IF (0 A) (0 N) (-1 Det) (1 A) (1 N) (2/* Pr) ; 	# dello stato tedesco del Baden-Württemberg
SELECT N IF (0 A) (0 N) (-1/* Pr) (1 A) (1 N) (2/* Pr) ; 	# dello stato tedesco del Baden-Württemberg
SELECT A IF (0 A) (0 N) (-2 Det) (-1 N) (1/* Pr) ; 		# dello stato tedesco del Baden-Württemberg
SELECT A IF (0 A) (0 N) (-2/* Pr) (-1 N) (1/* Pr) ; 		# dello stato tedesco del Baden-Württemberg
SELECT N IF (0 A) (0 N) (-1 Det) (1 A) (1 N) (2/* Det) ; 	# dello stato tedesco del Baden-Württemberg
SELECT N IF (0 A) (0 N) (-1/* Pr) (1 A) (1 N) (2/* Det) ; 	# dello stato tedesco del Baden-Württemberg
SELECT A IF (0 A) (0 N) (-2 Det) (-1 N) (1/* Det) ; 		# dello stato tedesco del Baden-Württemberg
SELECT A IF (0 A) (0 N) (-2/* Pr) (-1 N) (1/* Det) ; 		# dello stato tedesco del Baden-Württemberg

SELECT N IF (0 A) (0 N) (-1 Det) (1/* Pr) ; 			# sullo stato dei mari
SELECT N IF (0 A) (0 N) (-1/* Pr) (1/* Pr) ; 			# sullo stato dei mari
SELECT N IF (0 A) (0 N) (-1 Det) (1/* Det) ; 			# sullo stato dei mari
SELECT N IF (0 A) (0 N) (-1/* Pr) (1/* Det) ; 			# sullo stato dei mari

SELECT N IF (0 A) (0 N) (-1 Det) (1 A) ; 			# lo stato italiano
SELECT N IF (0 Adv) (0 N) (-1 Det) (1 A) ; 			# un'altra specie chimica

SELECT N IF (0 N) (0 PrfPrc) (-1C Det) (1 unknown) ;
SELECT N IF (0 N) (0 A) (-1C Det) (1 unknown) ;			# Secondo la rivista Motorrader
SELECT Npl IF (0 Npl) (0 PrfPrc) (-1C Num) (1 unknown) ;
SELECT Npl IF (0 Npl) (0 A) (-1C Num) (1 unknown) ;

#SELECT A IF (0 A) (0 N) (1 A) (-1C N) ; 			# Regno Lombardo-Veneto (no funciona: il presidente Franco Marini)

REMOVE A IF (0 A) (0 N) (-1 Pos) (1 EOS) ;		# loro sorte.
REMOVE PrfPrc IF (0 PrfPrc) (0 N) (-1 Pos) (1 EOS) ;	# loro sorte.
REMOVE Vconj IF (0 Vconj) (0 N) (-1C A) (1 EOS) ;	# le più grandi collezioni.
REMOVE Vconj IF (0 Vconj) (0 N) (-1C A) (1 Lpar) ;	# due nuove canzonii (di cui una

REMOVE Sg IF (0 Vconj) (0 N) (0 Sg) (-1C Num) (NOT -1 ("uno"));		# dieci canzoni

REMOVE A IF (0 A) (0 N) (-1 Pos) (1 Pr) ; 		# il suo futuro attraverso una programmazione
REMOVE PrfPrc IF (0 PrfPrc) (0 N) (-1 Pos) (1 Pr) ;
REMOVE A IF (0 A) (0 N) (-1 Det) (1 Pr) ; 		# il futuro della Ue.
REMOVE PrfPrc IF (0 PrfPrc) (0 N) (-1 Det) (1 Pr) ;

REMOVE PrfPrc IF (0 PrfPrc) (0 N) (-1C Pr) ;		# in corso "Pitretu è Bicchisgià"
REMOVE PrfPrc IF (0 PrfPrc) (0 A) (-1C Pr) ;

REMOVE V IF (0 V) (0 A) (-1C Det) ;		# i celebri laghi
REMOVE V IF (0 V) (0 N) (-1C Det) ;

REMOVE V IF (NOT -2 N) (-1C A + $$MascSg) (0 N + $$MascSg) (0 V) ;	# primo violino
REMOVE V IF (NOT -2 N) (-1C A + $$FemSg) (0 N + $$FemSg) (0 V) ;	# primo violino
REMOVE V IF (NOT -2 N) (-1C A + $$MascPl) (0 N + $$MascPl) (0 V) ;	# primo violino
REMOVE V IF (NOT -2 N) (-1C A + $$FemPl) (0 N + $$FemPl) (0 V) ;	# primo violino

REMOVE Ifi IF (-2 N) (-1 ("più") OR ("meno")) (0 Ifi OR A OR PrfPrc) ;		# lingue più diffuse
REMOVE Ifi IF (-3 N) (-2 A) (-1 ("più") OR ("meno")) (0 Ifi OR A OR PrfPrc) ;	# lingue internazionali più diffuse
SELECT A IF (-2 N) (-1 ("più") OR ("meno")) (0 A OR PrfPrc) ;			# lingue più diffuse
SELECT A IF (-3 N) (-2 A) (-1 ("più") OR ("meno")) (0 A OR PrfPrc) ;		# lingue internazionali più diffuse

SELECT A IF (0 Pos) (-1 Det) (1 N) ;	# il suo governo [regla necesaria porque "il suo" y similares no se cogen como un lema en ita-srd]
					# WARNING: No funciona per a "del suo governo" (igual que a dalt amb "nel mondo")
SELECT A IF (0 Pos) (-1/* Pr) (1 N) ;	# solució problema "del"

REMOVE V IF (0 N) (0 Vconj) (-1 Pr) ;	# la rivolta dei Fasci siciliani.
REMOVE V IF (0 N) (0 Vconj) (-1C Det) ;	# in un dato

REMOVE Vconj IF (0 Vconj) (0 N) (-1 BOS) (1C A) (2 EOS) ;	# Relazioni internazionali

SELECT N IF (0 ("guerra")) (1 ("civile")) ;	# la Guerra Civile
SELECT N IF (0 ("corso")) (1 NP) ;		# il corso Dante

#SELECT Interr IF (0 Interr) (0 CA) (-1 Vinterr) ;	# problema: il Principe Taik fu chiesto come pagamento
#SELECT Interr IF (0 Interr) (0 CA) (1C Vall) ;	# de moment comento aquesta regla que provoca molts canvis

SELECT A IF (0C A OR Adv) (-1 Det) (1 N) ; # una prima verzione

# Coordinació
SELECT M IF (0 N + M) (0 N + F) (-1 Det + M) ;	# un pirata, i calci, un file, sul fronte jugoslavo
SELECT F IF (0 N + M) (0 N + F) (-1 Det + F) ;
SELECT M IF (0 A + M) (0 A + F) (-1 Det + M) ;
SELECT F IF (0 A + M) (0 A + F) (-1 Det + F) ;

SELECT M IF (0 N + M) (0 N + F) (-1 A + M) (NOT -1 A + F) ;	# un suo moto
SELECT F IF (0 N + M) (0 N + F) (-1 A + F) (NOT -1 A + M) ;
SELECT M IF (0 N + M) (0 N + F) (1 A + M) (NOT 1 A + F) ;	# consigliere diplomatico
SELECT F IF (0 N + M) (0 N + F) (1 A + F) (NOT 1 A + M) ;
SELECT M IF (0 N + M) (0 N + F) (1 Adv) (2 A + M) (NOT 2 A + F) ;	# consigliere molto diplomatico
SELECT F IF (0 N + M) (0 N + F) (1 Adv) (2 A + F) (NOT 2 A + M) ;

#SELECT A IF (0 ADJ + $$MascSg) (0 Adv) (-1 N + $$MascSg)	# un suo moto proprio

SELECT P1 IF (0 ("<sono>")) (-1 Io) ;				# io sono
SELECT P1 IF (0 ("<sono>")) (-2 Io) (-1 Adv OR Pron) ;
SELECT P1 IF (0 ("<sono>")) (1 A + Sg OR PrfPrc + Sg) ;		# sono povero
SELECT P1 IF (0 ("<sono>")) (1 Adv) (2 A + Sg OR PrfPrc + Sg) ;	# sono molto povero

SELECT N IF (-1C Num OR Det) (0C N OR A) (1 Pr) ;			# due terzi di
SELECT N IF (-1C Num OR Det) (0C N OR A) (1 EOS) ;			# due terzi.
SELECT N IF (-1C Num OR Det) (0C N OR A) (1 CS) ;
SELECT N IF (-1C Num OR Det) (0C N OR A) (1 CA) ;
SELECT N IF (-2C Num OR Det) (-1C A) (0C N OR A) (1 Pr) ;		# il primo quarto
SELECT N IF (-2C Num OR Det) (-1C A) (0C N OR A) (1 EOS) ;
SELECT N IF (-2C Num OR Det) (-1C A) (0C N OR A) (1 CS) ;
SELECT N IF (-2C Num OR Det) (-1C A) (0C N OR A) (1 CA) ;

SELECT N IF (0 ("mezzo")) (1 ("di")) (2 ("comunicazione")) ;
SELECT N IF (0 ("capitale")) (1/* ("di")) ;
SELECT F IF (NOT -1 F) (0 ("capitale")) (0 M) (0 F) (1/* ("di")) ;
SELECT F IF (NOT -1 F) (0 ("capitale")) (0 M) (0 F) (1 NP) ;
SELECT N IF (-1 NP) (0 ("capitale"));
SELECT A IF (0 ("tale")) (1 N);					# per tale motivo


# NP

SELECT NP IF (0 NP) (NOT -1 BOS) (NOT 0 ("Sole") OR ("Guerra")) ;
	# Llevem paraules que, per una raó o altra, es posen en majúscules sense ser NP

SELECT N IF (-1 ("<il>")) (0 ("<Sole>")) ;		# il Sole
SELECT N IF (-1 ("<la>")) (0 ("<Guerra>")) ;		# la Guerra (Mundial, d'Independència, etc.)
SELECT N IF (-2 ("<la>")) (-1 A) (0 ("<Guerra>")) ;	# la Gran Guerra

SELECT NP IF (0 Top) (0 N OR A) (NOT -1C (ant) OR (cog));

REMOVE Cog IF (-1 In OR Da) (0 Top) (0 Cog)  ;
REMOVE Ant IF (-1 In OR Da) (0 Top) (0 Ant)  ;

SELECT Ant IF (0 Ant) (1C Ant) ;
        # Rosa Maria
SELECT Ant IF (-1 ("santo")) (0 Ant) (0 Cog OR Top) ;
        # sant Benet
SELECT Cog IF (0 Cog OR Ant) (-1C Ant) (NOT 1 segles_num_romans) (NOT 1 Cog) (NOT 1 ("di"));
SELECT Ant_Cog IF (0 Cog OR Ant) (1C Cog) ;
SELECT Ant IF (0 Ant) (0 Cog) (1 ("di")) (2 Top) ;
	# Alfonso di Portogallo
SELECT Top IF (-2 Ant) (-1 ("di")) (0 Ant OR Cog OR Top) ;
	# Ipparco di Nicea
SELECT Top IF (-3 Ant) (-2 segles_num_romans) (-1 ("di")) (0 Ant OR Cog OR Top) ;
	# Joan II di Castiglia
SELECT Top IF (-2 ("concilio") OR ("parlamento") OR ("statuto") OR ("costituzione") OR ("trattato") OR ("dichiarazione"))
	(-1 ("di")) (0 Ant OR Cog OR Top) ;
	# concilio di Nicea
SELECT Top IF (-2 ("corona") OR ("regno") OR ("contea") OR ("ducato")) (-1 ("di")) (0 Cog OR Top) ;
	# regno di Castiglia
SELECT Top IF (-2 Dignità) (-1 ("di")) (0 Ant OR Cog OR Top) ;
	# principe di Viana
SELECT Top IF (-2 ("andare") OR ("viaggiare")) (-1 ("a")) (0 Ant OR Cog OR Top) ;
REMOVE Top IF (NOT -2 ("andare") OR ("viaggiare")) (-1 ("a")) (0C Ant OR Cog OR Top) ;
SELECT Top IF (-3 ("andare") OR ("viaggiare")) (-2 ("a")) (-1 Det + Def) (0 Ant OR Cog OR Top) ;
REMOVE Top IF (NOT -3 ("andare") OR ("viaggiare")) (-2 ("a")) (-1 Det + Def) (0C Ant OR Cog OR Top) ;
SELECT Top IF (-2 NP) (-1 ("di")) (0 Ant OR Cog OR Top) ;

SELECT Top IF (-2C Top) (-1 Cm OR CC) (0 Top) (0 Cog OR Ant);
SELECT Top IF (2 Top) (1 Cm OR CC) (0 Top) (0 Cog OR Ant);

# Coincidència nom propi/comú. Ex. Fons Europeu, Guerra Civil...
REMOVE:nom_propicomu NP IF (0 NomPropiComu) (NOT -1C (ant) OR (cog) OR unknownMajIni); 

# Per a evitar la traducció de cognoms
# Posa com a lema la forma de la paraula marcada amb *
# Amb els adjectius hi ha més risc d'error (!)
SUBSTITUTE:nom_propi1 (".*"r) ("\\*$1"v) TARGET ("<(.*)>"r) IF (-1C (ant) OR (cog)) (0C Vall OR N OR A) (0 MajIni) (NOT 0 segles_num_romans) (NOT -1 ("Al")) ; 
REPLACE:nom_propi2 (np) Vall OR N OR A IF (-1C (ant) OR (cog)) (0C Vall OR N OR A) (0 MajIni) (NOT 0 segles_num_romans) (NOT -1 ("Al"));  # Això no sé si és útil. 
